buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.0"
    }
}

version "0.2-SNAPSHOT"
group 'org.grails.plugins'

apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin: "com.jfrog.artifactory"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    console "org.grails:grails-console"
    profile "org.grails.profiles:plugin"
    provided "org.grails:grails-plugin-services"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails:grails-plugin-domain-class"
    // To use JsonSlurper : http://stackoverflow.com/questions/11481014/why-cant-i-import-this-groovy-library-in-grails
    compile "org.codehaus.groovy:groovy-all:2.4.7"
}

grailsPublish {
    user = System.getenv("BINTRAY_USER") ?: project.bintrayUser
    key = System.getenv("BINTRAY_KEY") ?: project.bintrayKey
    portalUser = System.getenv("GRAILS_PORTAL_USER") ?: project.grailsPortalUser
    portalPassword = System.getenv("GRAILS_PORTAL_PASSWORD") ?: project.grailsPortalPassword

    githubSlug = 'sdelamo/grails-geoplugin'
    license {
        name = 'Apache-2.0'
    }
    title = "Grails GeoPlugin"
    desc = "Exposes a Grails service which queries the geoplugin api given an ip address"
    developers = [sdelamo:"Sergio del Amo"]

}

bintray {
    pkg {
        name = "$project.name"
        issueTrackerUrl = "https://github.com/sdelamo/$project.name/issues"
        vcsUrl = "https://github.com/sdelamo/$project.name"
        version {
            attributes = ['grails-plugin': "$project.group:$project.name"]
            name = project.version
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getenv("BINTRAY_USER") ?: project.bintrayUser
            password = System.getenv("BINTRAY_KEY") ?: project.bintrayKey
        }
        defaults {
            publications('maven')
        }
    }
}
